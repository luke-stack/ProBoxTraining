<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FB_TurnTableDtuEmulation" Id="{96c60269-4b08-4087-b17b-a5f4a6ab2dfd}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TurnTableDtuEmulation EXTENDS FB_FunctionConvEmulation
(**************************************************************************************
 * 	Application		:	BoxControl Framework
 *	Revision		:	$Revision:  $
 *	Revision date	:	$Date: $
 *	Last changed by	:	$Author:  $
 *	URL				:	$URL: $
 *
 *	Purpose			:	Emulation.
 *
 * ------------------------------------------------------------------------------------
 *	Copyright Swisslog [IP AG], Switzerland. All rights reserved.
 * 
 * ------------------------------------------------------------------------------------
 *	
 * 	Revision History: 
 *
 * 	VERSION			DATE			INITIALS			DESCRIPTION
 *	0.1				01.01.2001		swl					Example
 *
 **************************************************************************************)
VAR_INPUT
	Config							: ST_CFG_TurnTableDtuEmulation; // emulation config	
END_VAR 

VAR
	pFunction						: POINTER TO FB_TurnTableDtu ; // Address to function
	rFunction						: REFERENCE TO FB_TurnTableDtu; // Reference to function
	fbEncoder						: FB_PB_Element_Encoder();
	fbTransportControl 				: FB_PB_TransportControlEmulation; // Transport control emulation
	fbLiftControl 					: FB_LiftControlEmulation; // Lift control emulation
	fbTurnControl					: FB_TurnControlEmulation;
	DebugSensors					: ST_DebugSensors_TurnTableDtuEmulation;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(**************************************************************************************************************
   Initialization
***************************************************************************************************************)
A_Init();

(**************************************************************************************************************
   Update reference
***************************************************************************************************************)
A_UpdateReference();

(**************************************************************************************************************
   Emulation
***************************************************************************************************************)
// If emulation function is initialized
IF FunctionData.Init THEN

	// Update data of real object
	M_UpdateRealObjectData();
	
	// Handle external Subsystem-Subsystem interfaces
	A_ExternalSTI();
	
	//TODO: Move to Element???
	fbEncoder(	Run:=FunctionBase.rFunctionInterfaceReal.In.Element.State.DriveRunning OR FunctionBase.rFunctionInterfaceReal.In.Element.State.DriveRunning_34, // Feedback drive is running
				Speed:= ProBoxSystem.EMULATION_SPEED);	//speed [mm/s]
	
	// Call transport control 
	fbTransportControl(
		Param 					:= M_GetTransportControlParam(mZoneNumber := 1), // Parameter of transport control
		PrevTransportData 		:= M_GetPreviousTransportData(mZoneNumber := 1), // Previous transport data
		NextTransportData 		:= M_GetNextTransportData(mZoneNumber := 1), // Next transport data	
		TransportEnable			:= TRUE, // Transport enable	
		DriveRunning			:= fbEncoder.Run,		
		EncoderValue			:= fbEncoder.EncoderValue,
		ZoneData 				:= ZoneData[1]); // Zone data		
		
	// Call turn control
	fbTurnControl(
		Config := Config.TurnTableCfg,
        DriveRun_CW:= rFunction.HW_Outputs.Drive_Run_CW,
        DriveRun_CCW:= rFunction.HW_Outputs.Drive_Run_CCW);	
		
		
	// Call Lift control
	fbLiftControl(
		Config := Config.LiftCfg, 
		DriveRun_Lift := rFunction.HW_Outputs.Drive_Run_UpDown);	
		
						
	// Overwrite inputs
	A_OverWriteInputs();
END_IF]]></ST>
    </Implementation>
    <Action Name="A_Init" Id="{c701c352-7e6c-40e3-aea8-48fccf81b3dc}">
      <Implementation>
        <ST><![CDATA[(**************************************************************************************
 * 	NAME		A_Init
 * 	FUNCTION	Initialize emulated dummy function
 **************************************************************************************)

// If dummy function is not initialized
IF NOT FunctionData.Init Then
	// Check that linked dummy function number is valid
	IF eFunctionNumber > E_FunctionNumber.F_BEGIN_FUNCTION AND
	   eFunctionNumber < E_FunctionNumber.F_END_FUNCTION THEN   
		
		// Check if the real module is initialized
		IF F_IsFunctionInitialized(eFunctionNumber := eFunctionNumber) THEN
			// Module is initialized
			FunctionData.Init:= TRUE;
			
			// Send debug message
			DebugMsg := CONCAT('Initialization done. Element Function: ', FunctionRegistry[eFunctionNumber].FunctionName);
			fbDebugMsg.M_SendInfoMsg(DebugMsg);
			
			// Initialize real module
			F_InitializeFunction(eFunctionNumber := eFunctionNumber);
		END_IF
	END_IF
END_IF
]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_OverWriteInputs" Id="{a49a4085-a42b-4930-abbc-e7873a2e5005}">
      <Implementation>
        <ST><![CDATA[(**************************************************************************************
 * 	NAME		A_OverWriteInputs
 * 	FUNCTION	Overwrite inputs for emulation
 **************************************************************************************)
 
ZoneData[1].TransportControlData.TransportParam.ZoneLength := FunctionBase.rSettingsFunctionReal.Zone[1].Length[1];	 
 // Debug sensors or proceed with overwriting inputs
IF (DebugSensors.EnableSensorDebug) THEN
	rFunction.HW_Inputs := DebugSensors.HW_Inputs;
ELSE
	rFunction.HW_Inputs.CV.Sensor_Stop_12	:= fbTransportControl.M_TU_PresentState(
											mPosition := FunctionBase.rSettingsFunctionReal.Zone[1].Length[1] - FunctionBase.rSettingsFunctionReal.Zone[1].ZoneEndLengthToSide[2],
											mIndex => FunctionBase.IndexToRemove[1],
											mLeadingEdgePos => ZoneData[1].TransportControlData.TU_LeadingEdgePos);
	
	rFunction.HW_Inputs.CV.Sensor_Stop_21			:= 	rFunction.HW_Inputs.CV.Sensor_Stop_12;	
	rFunction.HW_Inputs.CV.Sensor_Stop_34			:= 	rFunction.HW_Inputs.CV.Sensor_Stop_12;
	rFunction.HW_Inputs.CV.Sensor_Stop_43			:= 	rFunction.HW_Inputs.CV.Sensor_Stop_12;		
	rFunction.HW_Inputs.TT.Signal_MotorUnitOK 		:= TRUE;
	rFunction.HW_Inputs.TT.Sensor_Pos_CCW 			:= fbTurnControl.Turn_Pos_CCW;	
	rFunction.HW_Inputs.TT.Sensor_Creep_CCW 		:= fbTurnControl.Turn_Pos_CCW;	
	rFunction.HW_Inputs.TT.Sensor_Pos_CW 			:= fbTurnControl.Turn_Pos_CW;
	rFunction.HW_Inputs.TT.Sensor_Creep_CW 			:=fbTurnControl.Turn_Pos_CW;
	rFunction.HW_Inputs.TT.Sensor_Pos_MIDDLE_CCW 	:= fbTurnControl.Turn_Pos_Middle_CCW;	
	rFunction.HW_Inputs.TT.Sensor_Pos_MIDDLE_CW 	:= fbTurnControl.Turn_Pos_Middle_CW;
	rFunction.HW_Inputs.Sensor_Overhang_12			:= TRUE;
	rFunction.HW_Inputs.Sensor_Overhang_21			:= TRUE;
	rFunction.HW_Inputs.Sensor_Overhang_34			:= TRUE;
	rFunction.HW_Inputs.Sensor_Overhang_43			:= TRUE;
	rFunction.HW_Inputs.Sensor_Pos_Down 			:= fbLiftControl.Lift_Pos_Down;
	rFunction.HW_Inputs.Sensor_Pos_Up 				:= fbLiftControl.Lift_Pos_Up;
	//rFunction.HW_Inputs.Signal_MotorUnitOK := fbMotorControlLift.MotorUnitOK;
END_IF
]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_UpdateReference" Id="{2c370df7-9063-4b0b-afb4-6bf41bb7ab57}">
      <Implementation>
        <ST><![CDATA[(**************************************************************************************
 * 	NAME		A_UpdateReference
 * 	FUNCTION	Update reference to real object
 **************************************************************************************)

// Check valid function number
IF eFunctionNumber > E_FunctionNumber.F_BEGIN_FUNCTION AND
   eFunctionNumber < E_FunctionNumber.F_END_FUNCTION THEN

	// Is reference valid  
	IF __ISVALIDREF(FunctionRegistry[eFunctionNumber].rFunction) THEN
		// Build address to reference
		pFunction := ADR(FunctionRegistry[eFunctionNumber].rFunction);

		// If address is possible
		IF pFunction <> 0 THEN
			// Build reference to memory
			rFunction REF= pFunction^;
		ELSE
			// Reinitialize module
			FunctionData.Init:= FALSE;
		END_IF
	ELSE
		// Reinitialize module
		FunctionData.Init:= FALSE;
	END_IF
END_IF
		]]></ST>
      </Implementation>
    </Action>
    <ObjectProperties>
      <XmlArchive>
        <Data>
          <o xml:space="preserve" t="UMLStereoTypeContainerObject">
            <v n="IsType" t="UMLType">BaseArea</v>
            <v n="Stereotype">""</v>
            <d n="Stereotypes" t="Hashtable" />
          </o>
        </Data>
        <TypeList>
          <Type n="Hashtable">System.Collections.Hashtable</Type>
          <Type n="String">System.String</Type>
          <Type n="UMLStereoTypeContainerObject">{30250973-b110-4e31-b562-c102e042dca4}</Type>
          <Type n="UMLType">{0197b136-405a-42ee-bb27-fd08b621d0cf}</Type>
        </TypeList>
      </XmlArchive>
    </ObjectProperties>
    <LineIds Name="FB_TurnTableDtuEmulation">
      <LineId Id="3" Count="50" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TurnTableDtuEmulation.A_Init">
      <LineId Id="2" Count="24" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TurnTableDtuEmulation.A_OverWriteInputs">
      <LineId Id="2" Count="5" />
      <LineId Id="39" Count="2" />
      <LineId Id="8" Count="8" />
      <LineId Id="20" Count="10" />
      <LineId Id="34" Count="1" />
      <LineId Id="37" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TurnTableDtuEmulation.A_UpdateReference">
      <LineId Id="2" Count="26" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>